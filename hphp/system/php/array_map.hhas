.function [skip_frame dyn_callable] array_map($func = no_args, $arr = one_arg, $res = one_array, ...$extra) {
          .numiters 2;

# If we get here, a value was supplied for $res (or we did not get an
# an array as $arr), so we bail out to the c++ implementation

call_cpp_impl_with_extra:
          FPushFuncD   4   "__SystemLib\\array_map"
          CGetL $func
          CGetL $arr
          CGetL $res
          CGetL $extra
          FCall <Unpack> 3 1 - "" ""
          RetC

call_cpp_impl:
          FPushFuncD   2   "__SystemLib\\array_map"
          CGetL $func
          CGetL $arr
          FCall <> 2 1 - "" ""
          RetC

no_args:  String "array_map() expects at least 2 parameters, 0 given"
          Jmp warning

one_arg:  String "array_map() expects at least 2 parameters, 1 given"
          Jmp warning

bad_func: String "array_map() expects parameter 1 to be a valid callback"
          Jmp warning
warning:  Cns "E_USER_WARNING"
          FCallBuiltin 2 2 "trigger_error"
          PopC
          Null
          RetC

convert_to_array:
# with a null func, we have to convert the input to an array
          IsTypeL $arr ArrLike
          JmpNZ ident
          CGetL $arr
          FPushObjMethodD 0 "toArray" NullThrows
          FCall <> 0 1 - "" ""
          RetC
ident:    CGetL $arr
          RetC

# if we get here, there was only one array,
# so we use the fast, php version
one_array:
          CGetL $arr
          InstanceOfD "HH\\KeyedTraversable"
          JmpZ call_cpp_impl
          IssetL $func
          JmpZ convert_to_array
          CGetL $func
          DecodeCufIter 0 bad_func
.try {
            NewLikeArrayL $arr 0
            SetL $res
            PopC

            CGetL $arr
            IterInitK 1 endloop $v $k

  .try 1 {
    loop_x:   FPushCufIter 1 0
              CGetL $v
              FCall <> 1 1 - "" ""
              BaseL $res Define
              SetM 0 EL:$k
              PopC
              IterNextK 1 loop_x $v $k
              Jmp endloop
  } .catch {
              IterFree 1
              Throw
  }
} .catch {
            CIterFree 0
            Throw
}

endloop:  CIterFree 0
          CGetL $res
          RetC
}
