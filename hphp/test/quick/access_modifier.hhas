#tests that fields and methods with != 1 access modifier fail verification

.main {
  .declvars $x;
  DefCls 0
  FPushCtorD 1 "A"
  String "foo"
  FPassCE 0 Any
  FCall 1
  PopR
  SetL $x
  PopC
  CGetL $x
  FPushObjMethodD 1 "set" NullThrows
  String "bar"
  FPassCE 0 Any
  FCall 1
  PopR
  FPushFuncD 1 "var_dump"
  CGetL $x
  FPushObjMethodD 0 "get" NullThrows
  FCall 0
  FPassR 0 Any
  FCall 1
  PopR
  Int 1
  RetC
}

.class A {
  .property [public private] attre =
    """N;""";
  .property blah =
    """N;""";
  .method [protected public] <"" N  > get() {
    CheckThis
    BaseH
    QueryM 0 CGet PT:"attre"
    RetC
  }
  .method <"" N  > set($x) {
    CheckThis
    CGetL $x
    BaseH
    SetM 0 PT:"attre"
    PopC
    Null
    RetC
  }
  .method [public] <"" N  > __construct($attre) {
    CGetL $attre
    BaseH
    SetM 0 PT:"attre"
    PopC
    Null
    RetC
  }
  .method [public] <"" N  > __destruct() {
    String ""
    BaseH
    SetM 0 PT:"attre"
    PopC
    Null
    RetC
  }
}
