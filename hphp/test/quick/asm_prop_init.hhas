#
# Some tests of property/constant initializers.
#

.class Init {
  .default_ctor;
  .const BAR = """s:4:"1242";""";
  .property [static public] foo = """s:11:"static prop";""";
  .property [public] nonstat = """s:25:"this is a normal property";""";
}

.class Uninit {
  .default_ctor;
  .const BAR = uninit;
  .property [static public] foo = uninit;
  .property [public] nonstat = uninit;

  .method [static public] 86cinit($which) {
    CGetL $which
    String "BAR"
    Same
    JmpZ wtf

    String "default BAR value"
    RetC

  wtf:
    String "unknown property"
    Fatal Runtime
  }

  .method [static public] 86sinit() {
    String "default ::foo value"
    InitProp "foo" Static

    Null
    RetC
  }

  .method [static public] 86pinit() {
    CheckProp "nonstat"
    JmpNZ isset
    String "code-created prop value"
    InitProp "nonstat" NonStatic

  isset:
    Null
    RetC
  }
}

.function do_test($who) {
  CGetL $who
  ClassGetC
  ClsCns "BAR"
  String "\n"
  Concat
  Print
  PopC

  String "foo"
  CGetL $who
  ClassGetC
  CGetS Any
  String "\n"
  Concat
  Print
  PopC

  CGetL $who
  ClassGetC
  NewObj
  Dup
  NullUninit
  FCallCtor <LockWhileUnwinding> 0 1 "" - "" ""
  PopC
  LockObj
  SetL $obj
  PopC

  String "nonstat"
  BaseL $obj Warn
  QueryM 1 CGet PC:0 Any
  String "\n"
  Concat
  Print
  PopC

  Null
  RetC
}

.function init_test {
  NullUninit
  NullUninit
    String "Init"
  FCallFuncD <> 1 1 "" - "" "do_test"
  RetC
}

.function uninit_test {
  NullUninit
  NullUninit
    String "Uninit"
  FCallFuncD <> 1 1 "" - "" "do_test"
  RetC
}

.function ["__EntryPoint"("""y:0:{}""")] main() {
  NullUninit
  NullUninit
  FCallFuncD <> 0 1 "" - "" "init_test"
  PopC
  NullUninit
  NullUninit
  FCallFuncD <> 0 1 "" - "" "uninit_test"
  PopC

  Int 1
  RetC
}
