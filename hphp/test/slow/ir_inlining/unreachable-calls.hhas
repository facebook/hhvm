.hh_file 1;
.main {
  NullUninit
  NullUninit
  NullUninit
  FCallFuncD <> 0 1 "" - "main"
  PopC

  String "DONE\n"
  Print
  RetC
}

.class [unique final persistent] Cls1 {
  .default_ctor;
  .method [public unique] foobaz($a, $b, $c, $d, $e) {
    CGetL $c
    RetC
  }
}

.class [unique final persistent] Cls2 {
  .default_ctor;
  .method [public unique] foobaz($a, $b, $c, $d, $e) {
    CGetL $b
    RetC
  }
}

.function test($obj, $type, $x) {
  CGetL $type
  JmpZ cls2

  CGetL $obj
  AssertRATStk 0 Obj=Cls1
  NullUninit
  NullUninit
  CGetL $x
  CGetL $x
  CGetL $x
  CGetL $x
  CGetL $x
  FCallObjMethodD <> 5 1 "" - "Cls1" NullThrows "foobaz"
  Jmp end

cls2:
  CGetL $obj
  AssertRATStk 0 Obj=Cls2
  NullUninit
  NullUninit
  CGetL $x
  CGetL $x
  CGetL $x
  CGetL $x
  CGetL $x
  FCallObjMethodD <> 5 1 "" - "Cls2" NullThrows "foobaz"
  Jmp end

end:
  RetC
}

.function test_outer($type, $x) {
  CGetL $type
  JmpZ cls2

  NewObjD "Cls1"
  Dup
  NullUninit
  NullUninit
  FCallCtor <LockWhileUnwinding> 0 1 "" - ""
  PopC
  LockObj
  SetL $obj
  PopC
  NullUninit
  NullUninit
  NullUninit
  CGetL $obj
  CGetL $type
  CGetL $x
  FCallFuncD <> 3 1 "" - "test"
  RetC

cls2:
  NewObjD "Cls2"
  Dup
  NullUninit
  NullUninit
  FCallCtor <LockWhileUnwinding> 0 1 "" - ""
  PopC
  LockObj
  SetL $obj
  PopC
  NullUninit
  NullUninit
  NullUninit
  CGetL $obj
  CGetL $type
  CGetL $x
  FCallFuncD <> 3 1 "" - "test"
  RetC
}

.function loop($type, $total) {
  Int 0
  SetL $count
  PopC

header:
  CGetL $count
  CGetL $total
  Lt
  JmpZ end

  NullUninit
  NullUninit
  NullUninit
  CGetL $type
  Int 1
  FCallFuncD <> 2 1 "" - "test_outer"
  PopC

  IncDecL $count PreInc
  PopC
  Jmp header

end:
  True
  RetC
}

.function main() {
  NullUninit
  NullUninit
  NullUninit
  False
  Int 40
  FCallFuncD <> 2 1 "" - "loop"
  PopC

  NullUninit
  NullUninit
  NullUninit
  True
  Int 100
  FCallFuncD <> 2 1 "" - "loop"
  RetC
}
