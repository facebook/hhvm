# Some tests for catch regions.

.main {
  FPushFuncD 0 "main"
  FCall 0
  PopR
  Int 1
  RetC
}

.function throw_stuff($msg) {
  .try {
    String "about to throw: "
    CGetL $msg
    Concat
    String "\n"
    Concat
    Print
    PopC

    FPushCtorD 1 "Exception"
    FPassL 0 $msg
    FCall 1
    PopR
    Throw
  } .catch {
    String "throw_stuff's exception handler ...\n"
    Print
    PopC
    Throw
  }
}

.function foo() {
  String "testing catch region\n"
  Print
  PopC

  .try {
    String "In region\n"
    Print
    PopC

    FPushFuncD 1 "throw_stuff"
    String "the message"
    FPassC 0
    FCall 1
    PopR

    String "unreached\n"
    Print
    PopC
    True
    RetC
  } .catch {
    String "In foo's exception handler\n"
    Print
    PopC
    Throw
  }
}

.class One extends Exception {}
.class Two extends Exception {}
.class Three extends Exception {}

.function throw_various() {
           Int 0
           SetL $counter
           PopC

repeat:
           .try {
             CGetL $counter
             Switch Unbounded 0 <make_one make_two make_three>

make_one:    FPushCtorD 0 "One"
             FCall 0
             PopR
             Throw
make_two:    FPushCtorD 0 "Two"
             FCall 0
             PopR
             Throw
make_three:  FPushCtorD 1 "Three"
             String "third exception from throw_various"
             FPassC 0
             FCall 1
             PopR
             Throw
           } .catch {
             Dup
             InstanceOfD "One"
             JmpNZ on_one
             Dup
             InstanceOfD "Two"
             JmpNZ on_two
             Dup
             InstanceOfD "Three"
             JmpNZ on_three
             Throw
on_one:      String "one\n"
             Print
             PopC
             Int 1
             SetL $counter
             PopC
             PopC
             Jmp repeat
on_two:      PopC
             String "two\n"
             Print
             PopC
             Int 2
             SetL $counter
             PopC
             Jmp repeat
on_three:    String "three\n"
             Print
             PopC
             Throw
           }
}

.function main() {
         False
         SetL $ever_caught
         PopC
         String "foo"

resume:  .try {
           FPushFunc 0
           FCall 0
           PopR
           Jmp resume2
         } .catch {
           SetL $ex
           PopC
           String "top-level catch handler got: "
           PushL $ex
           FPushObjMethodD 0 "getMessage" NullThrows
           FCall 0
           UnboxR
           Concat
           String "\n"
           Concat
           Print
           PopC
           # This might be illegal hhbc?
           CGetL $ever_caught
           True
           SetL $ever_caught
           PopC
           JmpNZ resume2
           String "throw_various"
           Jmp resume
         }

resume2: True
         RetC
}
