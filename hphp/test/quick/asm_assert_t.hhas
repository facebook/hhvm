#
# Simple test of AssertRAT opcodes
#

.main {
  FPushFuncD 0 "main"
  FCall 0 0 1 - "" ""
  PopR

  FPushCtorD 0 "Foo"
  FCall 0 0 1 - "" ""
  PopR
  SetL $x
  PopC

  FPushCtorD 0 "Bar"
  FCall 0 0 1 - "" ""
  PopR
  SetL $y
  PopC

  FPushFuncD 2 "obj_type"
  CGetL $x
  CGetL $y
  FCall 2 0 1 - "" ""
  PopR

  FPushFuncD 0 "obj_type2"
  FCall 0 0 1 - "" ""
  PopR

  Int 1
  RetC
}

.class Foo {
  .default_ctor;

  .method [public] returns_int {
    Int 12
    RetC
  }
}

.class Bar extends Foo {
  .default_ctor;
}

.function obj_type($x, $sub) {
  CGetL $x
  AssertRATStk 0 Obj=Foo
  AssertRATL $x Obj=Foo
  AssertRATL $sub Obj<=Bar
  PopC
  AssertRATL $x   Obj=Foo
  AssertRATL $x   Obj<=Foo
  AssertRATL $sub Obj=Bar
  AssertRATL $sub Obj<=Foo
  AssertRATL $sub Obj=Bar
  Int 1
  RetC
}

.function make_bar() {
  FPushCtorD 0 "Bar"
  FCall 0 0 1 - "" ""
  AssertRATStk 0 InitNull
  AssertRATStk 1 Obj
  AssertRATStk 1 Obj=Bar
  PopR
  AssertRATStk 0 Obj=Bar
  RetC
}

.function obj_type2() {
  FPushFuncD 0 "make_bar"
  FCall 0 0 1 - "" ""
  AssertRATStk 0 Obj
  AssertRATStk 0 Obj=Bar
  AssertRATL $x Uninit
  UnboxR
  AssertRATStk 0 Obj
  SetL $x
  PopC
  Int 1
  RetC
}

.function main() {
  Jmp stuff
out:
  Null
  RetC

stuff:
  FPushCtorD 0 "Foo"
  FCall 0 0 1 - "" ""
  PopR
  SetL $obj
  PopC

  CGetL $obj
  FPushObjMethodD 0 "returns_int" NullThrows
  FCall 0 0 1 - "" ""
  AssertRATStk 0 Int
  UnboxR
  Int 0
  Add
  SetL $k
  PopC
  Jmp out
}

