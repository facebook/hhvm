#
# Try out dv initializers in the assembler
#

.main {
  String "beginning...\n"
  Print
  PopC

  NullUninit
  NullUninit
  NullUninit
  FPushFuncD 0 "main"
  FCall <> 0 1 "" - "" ""
  RetC
}

.function foo($x, $y = y_init) {
  entry:
          CGetL $x
          CGetL $y
          Concat
          Print
          RetC

  y_init: String " default Y value\n"
          SetL $y
          PopC
          Jmp entry
}

.function multiple($x = x_init,
                   $y = y_init,
                   $z = z_init) {
  entry:  String "x = "
          CGetL $x
          Concat
          Print
          PopC

          String "y = "
          CGetL $y
          Concat
          Print
          PopC

          String "z = "
          CGetL $z
          Concat
          Print
          PopC

          True
          RetC

  x_init: String "defaultx\n"
          SetL $x
          PopC
  y_init: String "defaulty\n"
          SetL $y
          PopC
  z_init: String "defaultz\n"
          SetL $z
          PopC
          Jmp entry
}

.function main() {
  NullUninit
  NullUninit
  NullUninit
  String "sup: "
  FPushFuncD 1 "foo"
  FCall <> 1 1 "" - "" ""
  PopC

  NullUninit
  NullUninit
  NullUninit
  String "sup: "
  String " non-default y value\n"
  FPushFuncD 2 "foo"
  FCall <> 2 1 "" - "" ""
  PopC

  NullUninit
  NullUninit
  NullUninit
  FPushFuncD 0 "multiple"
  FCall <> 0 1 "" - "" ""
  PopC
  NullUninit
  NullUninit
  NullUninit
  String "nondef\n"
  FPushFuncD 1 "multiple"
  FCall <> 1 1 "" - "" ""
  PopC
  NullUninit
  NullUninit
  NullUninit
  String "nondef1\n"
  String "nondef2\n"
  FPushFuncD 2 "multiple"
  FCall <> 2 1 "" - "" ""
  PopC

  String "done\n"
  Print
  RetC
}
