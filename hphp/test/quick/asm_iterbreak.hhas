.function ["__EntryPoint"("""y:0:{}""")] N main() {
  .numiters 2;
  .declvars $0 $1 $2 $3 $4;
  NewDictArray 3

  String "hello"
  String "world"
  AddElemC

  String "this"
  String "is"
  AddElemC

  String "a"
  String "test"
  AddElemC

  SetL $0
  PopC

  CGetL $0
  IterInit 0 K:$2 V:$1 endOuter
  .try {
  startOuter:

    String "\n"
    CGetL2 $1
    Concat
    Print
    PopC

    String "\n"
    CGetL2 $2
    Concat
    Print
    PopC

    CGetL $0
    IterInit 1 K:$4 V:$3 endInner
    .try {
    startInner:
      String "\n"
      CGetL2 $3
      Concat
      Print
      PopC

      String "\n"
      CGetL2 $4
      Concat
      Print
      PopC

      IterFree 1
      IterFree 0
      Jmp endOuter
      IterNext 1 K:$4 V:$3 startInner
      Jmp endInner
    } .catch {
      IterFree 1
      Throw
    }
  endInner:
    IterNext 0 K:$2 V:$1 startOuter
    Jmp endOuter
  } .catch {
    IterFree 0
    Throw
  }
endOuter:
  Int 1
  RetC
}
