include(ExternalProject)
include(HPHPFunctions)

get_target_property(BOOST_INCLUDE_DIR boost INTERFACE_INCLUDE_DIRECTORIES)
get_target_property(BOOST_LIBRARIES boost INTERFACE_LINK_LIBRARIES)
list(GET BOOST_LIBRARIES 0 FIRST_LIB)
if("${FIRST_LIB}" MATCHES ".+/${CMAKE_STATIC_LIBRARY_PREFIX}boost_.+${CMAKE_STATIC_LIBRARY_SUFFIX}$")
  set(Boost_USE_STATIC_LIBS ON)
else()
  set(Boost_USE_STATIC_LIBS OFF)
endif()
get_filename_component(BOOST_LIBRARY_DIR "${FIRST_LIB}" DIRECTORY)

get_target_property(DOUBLE_CONVERSION_INCLUDE_DIR double-conversion INTERFACE_INCLUDE_DIRECTORIES)
get_target_property(DOUBLE_CONVERSION_LIBRARY double-conversion INTERFACE_LINK_LIBRARIES)
get_target_property(ZSTD_INCLUDE_DIR zstd INTERFACE_INCLUDE_DIRECTORIES)
get_target_property(ZSTD_LIBRARY zstd INTERFACE_LINK_LIBRARIES)
get_target_property(JEMALLOC_INCLUDE_DIR jemalloc INTERFACE_INCLUDE_DIRECTORIES)


SET_HHVM_THIRD_PARTY_SOURCE_ARGS(
  WANGLE_SOURCE_ARGS
  SOURCE_URL
  "https://github.com/facebook/wangle/releases/download/v2022.01.31.00/wangle-v2022.01.31.00.tar.gz"
  SOURCE_HASH
  "SHA256=1002e9c32b6f4837f6a760016e3b3e22f3509880ef3eaad191c80dc92655f23f"
)

ExternalProject_Add(
  bundled_wangle
  ${WANGLE_SOURCE_ARGS}
  PATCH_COMMAND
    "${CMAKE_COMMAND}" -E copy
    "${CMAKE_SOURCE_DIR}/CMake/FindLibSodium.cmake"
    <SOURCE_DIR>/wangle/cmake/FindLibSodium.cmake
    &&
    "${CMAKE_COMMAND}" -E copy
    "${CMAKE_SOURCE_DIR}/CMake/FindGlog.cmake"
    <SOURCE_DIR>/wangle/cmake/FindGlog.cmake
  SOURCE_SUBDIR wangle
  CMAKE_ARGS
    "-DCMAKE_PREFIX_PATH=${CMAKE_PREFIX_PATH}"
    -DCMAKE_MODULE_PATH=${CMAKE_SOURCE_DIR}/CMake
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_INSTALL_INCLUDEDIR=include

    -DBUILD_TESTS=OFF
    -DBUILD_EXAMPLES=OFF

    -DOPENSSL_INCLUDE_DIR=${OPENSSL_INCLUDE_DIR}
    -DOPENSSL_CRYPTO_LIBRARY=${OPENSSL_CRYPTO_LIBRARY}
    -DBoost_NO_SYSTEM_PATHS=ON
    -DBoost_NO_BOOST_CMAKE=ON
    -DBoost_USE_STATIC_LIBS=${Boost_USE_STATIC_LIBS}
    -DBoost_INCLUDE_DIR=${BOOST_INCLUDE_DIR}
    "-DBOOST_LIBRARYDIR=${BOOST_LIBRARY_DIR}"
    -DDOUBLE_CONVERSION_INCLUDE_DIR=${DOUBLE_CONVERSION_INCLUDE_DIR}
    -DDOUBLE_CONVERSION_LIBRARY=${DOUBLE_CONVERSION_LIBRARY}
    -DLIBGLOG_INCLUDE_DIR=${LIBGLOG_INCLUDE_DIR}
    -DLIBGLOG_LIBRARY=${LIBGLOG_LIBRARY}
    -DZSTD_INCLUDE_DIR=${ZSTD_INCLUDE_DIR}
    -DZSTD_LIBRARY_RELEASE=${ZSTD_LIBRARY}

    "-Dfizz_DIR=${FIZZ_INSTALL_DIR}/lib/cmake/fizz"
    "-Dfmt_DIR=${FMT_INSTALL_DIR}/lib/cmake/fmt"
    "-Dfolly_DIR=${FOLLY_INSTALL_DIR}/lib/cmake/folly"

    "-DCMAKE_CXX_FLAGS=-I${JEMALLOC_INCLUDE_DIR}"
)
ExternalProject_Get_property(bundled_wangle INSTALL_DIR)

set(
  WANGLE_DEPS
  fizz
  fmt
  folly
)
add_dependencies(bundled_wangle ${WANGLE_DEPS})
add_library(wangle INTERFACE)
add_dependencies(wangle bundled_wangle)
target_include_directories(wangle INTERFACE "${INSTALL_DIR}/include")
target_link_libraries(
  wangle
  INTERFACE
  ${WANGLE_DEPS} ${OPENSSL_LIBRARIES}
  "${INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}wangle${CMAKE_STATIC_LIBRARY_SUFFIX}"
)
set(WANGLE_INSTALL_DIR "${INSTALL_DIR}" PARENT_SCOPE)
