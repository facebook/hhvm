.hh_file 1;
#
# Test arrays for hhbc asm.
#

.main {
  NullUninit
  NullUninit
  NullUninit
  String "main"
  FCallFunc <> 0 1 "" - "" # allow the JIT why not
  PopC

  Int 1
  RetC
}

.adata my_array = """a:2:{s:3:"foo";s:3:"bar";s:3:"baz";s:4:"blah";}""";

.class ClassWithArray {
  .property [static public] arr =
            """a:4:{i:0;i:1;i:1;i:2;i:2;i:3;i:3;i:4;}""";
}

.function ArrayMember() {
  NullUninit
  NullUninit
  NullUninit
    String "arr"
    String "ClassWithArray"
    ClassGetC
    CGetS
  FCallFuncD <> 1 1 "" - "" "var_dump"
  PopC

  String "ClassWithArray"
  SetL $x
  PopC

  NullUninit
  NullUninit
  NullUninit
    String "arr"
    Int 2
    CGetL $x
    ClassGetC
    BaseSC 2 0 Warn
    QueryM 3 CGet EC:1
  FCallFuncD <> 1 1 "" - "" "var_dump"
  PopC

  True
  RetC
}

.function main() {
  NullUninit
  NullUninit
  NullUninit
  FCallFuncD <> 0 1 "" - "" "ArrayMember"
  PopC

  Array @my_array
  SetL $arr
  PopC

  BaseL $arr Warn
  QueryM 0 CGet ET:"foo"
  String "\n"
  Concat
  Print
  PopC

  String "one"
  BaseL $arr Define
  SetM 0 EI:1
  PopC

  BaseL $arr Warn
  QueryM 0 CGet EI:1
  String "\n"
  Concat
  Print
  PopC

  String "baz"
  SetL $key
  PopC
  BaseL $arr Unset
  UnsetM 0 EL:$key

  # Test that it's unset
  CGetL $arr
  BaseC 0 Warn
  QueryM 1 Isset EL:$key
  JmpZ unset_label

  String "This should be impossible...\n"
  Print
  PopC

  Null
  RetC

unset_label:  String "win\n"
              Print
              PopC

              NullUninit
              NullUninit
              NullUninit
              CGetL $arr
              FCallFuncD <> 1 1 "" - "" "var_dump"
              PopC

              NullUninit
              NullUninit
              NullUninit
              Int 1
              String "b"
              NewPackedArray 2
              FCallFuncD <> 1 1 "" - "" "var_dump"
              PopC

              Null
              RetC
}
