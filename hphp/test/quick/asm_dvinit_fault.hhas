#
# Try/catch in a DV initializer.
#
# HHVM doesn't generate code like this, but it's legal bytecode.
#

.main {
  FPushFuncD 1 "var_dump"
    FPushFuncD 0 "main"
    FCall <> 0 1 "" - "" ""
  FCall <> 1 1 "" - "" ""
  PopC
  Int 1
  RetC
}

.function main($x = dv_init1, $y = dv_init2) {
main_entry:
  CGetL $x
  CGetL2 $y
  Add
  RetC

dv_init1:
  Int 0
  SetL $x
  PopC
dv_init2:
  .try {
    .try {
      Int 1
      SetL $y
      PopC

      NewObjD "Exception"
      Dup
      FPushCtor 0
      FCall <> 0 1 "" - "" ""
      PopC
      Throw
    } .catch {
      Int 4321
      SetL $x
      PopC
      Throw
    }
  } .catch {
    PopC
    Jmp out_of_catch
  }

out_of_catch:
  Jmp main_entry
}
