.hh_file 1;
# SSwitch with integer-like strings uses a different translator
# implementation.

.main {
  NullUninit
  NullUninit
  NullUninit
  FCallFuncD <> 0 1 "" - "main"
  RetC
}

.function get_three() {
  String "3"
  RetC
}

.function sswitch_on($foo) {
  CGetL $foo
  SSwitch <"1":label_1
           "2":label_2
           "3":label_3
             -:fail>

label_1:
  String "1\n"
  Jmp done

label_2:
  String "2\n"
  Jmp done

label_3:
  String "3\n"

done:
  Print
  RetC

fail:
  String "default case\n"
  Print
  RetC
}

.function case_three() {
  NullUninit
  NullUninit
  NullUninit
  FCallFuncD <> 0 1 "" - "get_three"
  SetL $foo
  PopC

  NullUninit
  NullUninit
  NullUninit
  CGetL $foo
  FCallFuncD <> 1 1 "" - "sswitch_on"
  RetC
}

.class Obj {
  .default_ctor;
  .method [public] __toString() {
    String "2"
    RetC
  }
}

.function with_obj() {
  NewObjD "Obj"
  Dup
  NullUninit
  NullUninit
  FCallCtor <LockWhileUnwinding> 0 1 "" - ""
  PopC
  LockObj
  SetL $obj
  PopC

  NullUninit
  NullUninit
  NullUninit
  CGetL $obj
  FCallFuncD <> 1 1 "" - "sswitch_on"
  RetC
}

.function case_default() {
  NullUninit
  NullUninit
  NullUninit
  String "kujhasekguheku"
  FCallFuncD <> 1 1 "" - "sswitch_on"
  RetC
}

.function main() {
  NullUninit
  NullUninit
  NullUninit
  FCallFuncD <> 0 1 "" - "case_three"
  PopC

  NullUninit
  NullUninit
  NullUninit
  FCallFuncD <> 0 1 "" - "with_obj"
  PopC

  NullUninit
  NullUninit
  NullUninit
  FCallFuncD <> 0 1 "" - "case_default"
  PopC

  String "Done\n"
  Print
  RetC
}
