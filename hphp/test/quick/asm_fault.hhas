# Some tests for catch regions.

.function N throw_stuff(N $msg) {
  .try {
    String "about to throw: "
    CGetL $msg
    Concat
    String "\n"
    Concat
    Print
    PopC

    NewObjD "Exception"
    Dup
    NullUninit
    CGetL $msg
    FCallCtor <LockWhileUnwinding> 1 1 "" "" - "" ""
    PopC
    LockObj
    Throw
  } .catch {
    String "throw_stuff's exception handler ...\n"
    Print
    PopC
    Throw
  }
}

.function N foo() {
  String "testing catch region\n"
  Print
  PopC

  .try {
    String "In region\n"
    Print
    PopC
    NullUninit
    NullUninit
    String "the message"
    FCallFuncD <> 1 1 "" "" - "" "throw_stuff"
    PopC

    String "unreached\n"
    Print
    PopC
    True
    RetC
  } .catch {
    String "In foo's exception handler\n"
    Print
    PopC
    Throw
  }
}

.class One extends Exception {
}
.class Two extends Exception {
}
.class Three extends Exception {
}

.function N throw_various() {
  .declvars $counter;
           Int 0
           SetL $counter
           PopC

repeat:
           .try {
             CGetL $counter
             Switch Unbounded 0 <make_one make_two make_three>

make_one:    NewObjD "One"
             Dup
             NullUninit
             FCallCtor <LockWhileUnwinding> 0 1 "" "" - "" ""
             PopC
             LockObj
             Throw
make_two:    NewObjD "Two"
             Dup
             NullUninit
             FCallCtor <LockWhileUnwinding> 0 1 "" "" - "" ""
             PopC
             LockObj
             Throw
make_three:  NewObjD "Three"
             Dup
             NullUninit
             String "third exception from throw_various"
             FCallCtor <LockWhileUnwinding> 1 1 "" "" - "" ""
             PopC
             LockObj
             Throw
           } .catch {
             Dup
             InstanceOfD "One"
             JmpNZ on_one
             Dup
             InstanceOfD "Two"
             JmpNZ on_two
             Dup
             InstanceOfD "Three"
             JmpNZ on_three
             Throw
on_one:      String "one\n"
             Print
             PopC
             Int 1
             SetL $counter
             PopC
             PopC
             Jmp repeat
on_two:      PopC
             String "two\n"
             Print
             PopC
             Int 2
             SetL $counter
             PopC
             Jmp repeat
on_three:    String "three\n"
             Print
             PopC
             Throw
           }
}

.function ["__EntryPoint"("""y:0:{}""")] N main() {
  .declvars $ever_caught $ex;
         False
         SetL $ever_caught
         PopC
         NullUninit
         NullUninit
         String "foo"

resume:  .try {
           FCallFunc <> 0 1 "" "" - ""
           PopC
           Jmp resume2
         } .catch {
           SetL $ex
           PopC
           String "top-level catch handler got: "
           PushL $ex
           NullUninit
           FCallObjMethodD <> 0 1 "" "" - "" "" NullThrows "getMessage"
           Concat
           String "\n"
           Concat
           Print
           PopC
           # This might be illegal hhbc?
           CGetL $ever_caught
           True
           SetL $ever_caught
           PopC
           JmpNZ resume2
           NullUninit
           NullUninit
           String "throw_various"
           Jmp resume
         }

resume2: True
         RetC
}
