# Test type annotations for hhas

.adata A_0 = """a:0:{}""";

.main {
  DefCls 0
  FPushFuncD 0 "test"
  FCall <> 0 1 "" - "" ""
  PopC
  Int 1
  RetC
}

.function use_class(<"@C" "C" hh_type extended_hint soft> $x) {
  VerifyParamType $x
  Null
  RetC
}

.function use_int_soft(<"@HH\\int" "HH\\int" hh_type extended_hint soft> $x) {
  VerifyParamType $x
  Null
  RetC
}

.function use_int_nullable(<"?HH\\int" "HH\\int" nullable hh_type extended_hint> $x) {
  VerifyParamType $x
  Null
  RetC
}

.function use_int(<"HH\\int" "HH\\int" hh_type> $x) {
  VerifyParamType $x
  Null
  RetC
}

.function use_mixed(<"HH\\mixed" N> $x) {
  Null
  RetC
}

.function test() {
  FPushFuncD 1 "use_class"
  NewObjD "C"
  Dup
  FPushCtor 0
  FCall <> 0 1 "" - "" ""
  PopC
  FCall <> 1 1 "" - "" ""
  PopC

  FPushFuncD 1 "use_class"
  Null
  FCall <> 1 1 "" - "" ""
  PopC

  FPushFuncD 1 "use_int_soft"
  Int 5
  FCall <> 1 1 "" - "" ""
  PopC

  FPushFuncD 1 "use_int_soft"
  Array @A_0
  FCall <> 1 1 "" - "" ""
  PopC

  FPushFuncD 1 "use_int_soft"
  Null
  FCall <> 1 1 "" - "" ""
  PopC

  FPushFuncD 1 "use_int_nullable"
  Int 6
  FCall <> 1 1 "" - "" ""
  PopC

  FPushFuncD 1 "use_int_nullable"
  Null
  FCall <> 1 1 "" - "" ""
  PopC

  FPushFuncD 1 "use_int"
  Int 5
  FCall <> 1 1 "" - "" ""
  PopC

  FPushFuncD 1 "use_mixed"
  Array @A_0
  FCall <> 1 1 "" - "" ""
  PopC

  FPushFuncD 1 "use_int"
  Array @A_0
  FCall <> 1 1 "" - "" ""
  PopC

  Null
  RetC
}

.class C {
  .method [public static] <"HH\\int" "HH\\int" hh_type extended_hint> test() {
    Int 42
    RetC
  }
}
