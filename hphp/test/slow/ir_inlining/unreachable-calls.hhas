.main {
  FPushFuncD 0 "main"
  FCall 0
  PopR

  String "DONE\n"
  Print
  RetC
}

.class [unique final persistent] Cls1 {
  .default_ctor;
  .method [public unique] foobaz($a, $b, $c, $d, $e) {
    CGetL $c
    RetC
  }
}

.class [unique final persistent] Cls2 {
  .default_ctor;
  .method [public unique] foobaz($a, $b, $c, $d, $e) {
    CGetL $b
    RetC
  }
}

.function test($obj, $type, $x) {
  CGetL $type
  JmpZ cls2

  CGetL $obj
  AssertRATStk 0 Obj=Cls1
  FPushObjMethodD 5 "foobaz" NullThrows
  FPassL 0 $x Any
  FPassL 1 $x Any
  FPassL 2 $x Any
  FPassL 3 $x Any
  FPassL 4 $x Any
  FCallD 5 "Cls1" "foobaz"
  UnboxRNop
  Jmp end

cls2:
  CGetL $obj
  AssertRATStk 0 Obj=Cls2
  FPushObjMethodD 5 "foobaz" NullThrows
  FPassL 0 $x Any
  FPassL 1 $x Any
  FPassL 2 $x Any
  FPassL 3 $x Any
  FPassL 4 $x Any
  FCallD 5 "Cls2" "foobaz"
  UnboxRNop
  Jmp end

end:
  RetC
}

.function test_outer($type, $x) {
  CGetL $type
  JmpZ cls2

  FPushCtorD 0 "Cls1"
  FCall 0
  PopR
  SetL $obj
  PopC
  FPushFuncD 3 "test"
  FPassL 0 $obj Any
  FPassL 1 $type Any
  FPassL 2 $x Any
  FCall 3
  UnboxRNop
  RetC

cls2:
  FPushCtorD 0 "Cls2"
  FCall 0
  PopR
  SetL $obj
  PopC
  FPushFuncD 3 "test"
  FPassL 0 $obj Any
  FPassL 1 $type Any
  FPassL 2 $x Any
  FCall 3
  UnboxRNop
  RetC
}

.function loop($type, $total) {
  Int 0
  SetL $count
  PopC

header:
  CGetL $count
  CGetL $total
  Lt
  JmpZ end

  FPushFuncD 2 "test_outer"
  FPassL 0 $type Any
  Int 1
  FPassC 1 Any
  FCall 2
  PopR

  IncDecL $count PreInc
  PopC
  Jmp header

end:
  True
  RetC
}

.function main() {
  FPushFuncD 2 "loop"
  False
  FPassC 0 Any
  Int 40
  FPassC 1 Any
  FCall 2
  PopR

  FPushFuncD 2 "loop"
  True
  FPassC 0 Any
  Int 100
  FPassC 1 Any
  FCall 2
  UnboxRNop
  RetC
}
