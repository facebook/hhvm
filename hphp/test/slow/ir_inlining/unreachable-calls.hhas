.main {
  FPushFuncD 0 "main"
  FCall 0 0 1 - "" ""
  PopR

  String "DONE\n"
  Print
  RetC
}

.class [unique final persistent] Cls1 {
  .default_ctor;
  .method [public unique] foobaz($a, $b, $c, $d, $e) {
    CGetL $c
    RetC
  }
}

.class [unique final persistent] Cls2 {
  .default_ctor;
  .method [public unique] foobaz($a, $b, $c, $d, $e) {
    CGetL $b
    RetC
  }
}

.function test($obj, $type, $x) {
  CGetL $type
  JmpZ cls2

  CGetL $obj
  AssertRATStk 0 Obj=Cls1
  FPushObjMethodD 5 "foobaz" NullThrows
  CGetL $x
  CGetL $x
  CGetL $x
  CGetL $x
  CGetL $x
  FCall 5 0 1 - "Cls1" "foobaz"
  UnboxRNop
  Jmp end

cls2:
  CGetL $obj
  AssertRATStk 0 Obj=Cls2
  FPushObjMethodD 5 "foobaz" NullThrows
  CGetL $x
  CGetL $x
  CGetL $x
  CGetL $x
  CGetL $x
  FCall 5 0 1 - "Cls2" "foobaz"
  UnboxRNop
  Jmp end

end:
  RetC
}

.function test_outer($type, $x) {
  CGetL $type
  JmpZ cls2

  FPushCtorD 0 "Cls1"
  FCall 0 0 1 - "" ""
  PopR
  SetL $obj
  PopC
  FPushFuncD 3 "test"
  CGetL $obj
  CGetL $type
  CGetL $x
  FCall 3 0 1 - "" ""
  UnboxRNop
  RetC

cls2:
  FPushCtorD 0 "Cls2"
  FCall 0 0 1 - "" ""
  PopR
  SetL $obj
  PopC
  FPushFuncD 3 "test"
  CGetL $obj
  CGetL $type
  CGetL $x
  FCall 3 0 1 - "" ""
  UnboxRNop
  RetC
}

.function loop($type, $total) {
  Int 0
  SetL $count
  PopC

header:
  CGetL $count
  CGetL $total
  Lt
  JmpZ end

  FPushFuncD 2 "test_outer"
  CGetL $type
  Int 1
  FCall 2 0 1 - "" ""
  PopR

  IncDecL $count PreInc
  PopC
  Jmp header

end:
  True
  RetC
}

.function main() {
  FPushFuncD 2 "loop"
  False
  Int 40
  FCall 2 0 1 - "" ""
  PopR

  FPushFuncD 2 "loop"
  True
  Int 100
  FCall 2 0 1 - "" ""
  UnboxRNop
  RetC
}
